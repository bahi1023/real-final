resource "aws_apigatewayv2_api" "main" {
  name          = "${var.project_name}-api-v2-${var.environment}"
  protocol_type = "HTTP"
  description   = "HTTP API Gateway with JWT Auth"

  tags = {
    Project     = var.project_name
    Environment = var.environment
  }
}

resource "aws_apigatewayv2_stage" "default" {
  api_id = aws_apigatewayv2_api.main.id
  name   = "$default"
  auto_deploy = true
}

resource "aws_apigatewayv2_vpc_link" "main" {
  name               = "${var.project_name}-vpc-link"
  security_group_ids = [aws_eks_cluster.main.vpc_config[0].cluster_security_group_id]
  subnet_ids         = aws_subnet.private[*].id

  tags = {
    Project = var.project_name
  }
}

resource "aws_apigatewayv2_authorizer" "jwt" {
  api_id           = aws_apigatewayv2_api.main.id
  authorizer_type  = "JWT"
  identity_sources = ["$request.header.Authorization"]
  name             = "CognitoJWT"

  jwt_configuration {
    audience = [aws_cognito_user_pool_client.client.id]
    issuer   = "https://cognito-idp.${var.aws_region}.amazonaws.com/${aws_cognito_user_pool.main.id}"
  }
}

resource "aws_apigatewayv2_integration" "nlb_proxy" {
  api_id           = aws_apigatewayv2_api.main.id
  integration_type = "HTTP_PROXY"
  
  # This variable is updated by the pipeline (sed) after Helm creates the LB
  integration_uri  = var.nlb_dns_name 
  
  integration_method = "ANY"
  connection_type    = "INTERNET"

  lifecycle {
    ignore_changes = [integration_uri]
  }
}
resource "aws_apigatewayv2_route" "default" {
  api_id    = aws_apigatewayv2_api.main.id
  route_key = "$default"
  target    = "integrations/${aws_apigatewayv2_integration.nlb_proxy.id}"

  authorization_type = "NONE"
}
